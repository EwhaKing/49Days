title: 일차0_밤
---
<<declare $playerName = "">>
<<change_bgm night_1>>
<<enter_and_sit 키루스 1 >>
<<change_sprite 키루스 무표정>>
<<zoom 1>>
<<wait 2>>
키루스: 자, 여기가 바로 네가 오늘부터 일할 찻집이야.
<<change_sprite 키루스 놀람>>
키루스: 그나저나 그 가면... 낯이 익은데.
키루스: 네가 윤회를 거부당했다던 자인가?
<<change_sprite 키루스 무표정>>
키루스: 이거 영광이네. 유명인사를 만나다니.
키루스: 키루스야.
-> 안녕하세요.
-> ...
키루스: 그래서... 녹차 하나는 제대로 우릴 수 있는 거야?
<<add_success_tea greentea>>
-> 아니요. (튜토리얼 진행)
    <<change_sprite 키루스 분노>>
    키루스: 정말 손이 많이 가는 망자네.
    키루스: 주방으로 가봐.
    <<set_kitchen_node_title 튜토리얼_주방>>
-> 당연하죠.
    <<change_sprite 키루스 미소>>
    키루스: 듣던 중 반가운 소리네.
    키루스: 60도에 5초, 알지?
    키루스: 간단하게 녹차부터 시작해봐.
<<order 일차0_밤1 false>>
===

title: 튜토리얼_주방
---
//주방으로 화면 전환
//찬장 쪽으로 이동
<<move_cyrus -9.61 4.82>>
키루스: 여기 찻잎 통이 보이지?
키루스: 우선 찬장에서 찻잎을 꺼낼 거야.
//산화기로 이동
<<move_cyrus 1.6 -1.9>>
키루스: 이건 산화기야.
키루스: 백차나 우롱차, 홍차 같은 걸 우릴 땐 필요하겠지만...
키루스: 녹차는 산화시키지 않으니 넘어가자고.
//옆 화면으로 이동
<<move_camera>>
<<move_cyrus -25.6 -3>>
키루스: 스페이스 바를 눌러 옆으로 이동할 수 있어.
//가마솥으로 이동
<<move_cyrus -28.6 0.38>>
키루스: 이 검은 솥이 보이지? 찻잎을 덖을 수 있어.
//바구니로 이동
<<move_cyrus -18.65 0.45>>
키루스: 여기 바구니에 찻잎을 넣으면 유념할 수 있지.
//옆 화면으로 이동
<<move_camera>>
//다병으로 이동
<<move_cyrus -5.36 -4>>
키루스: 이건 다병이라고 불러.
키루스: 가공된 찻잎이나 재료를 여기에 넣고 우릴 거야.
//주전자 쪽으로 이동
<<move_cyrus 10 -0.5>>
키루스: 주전자와 화로가 보이지?
키루스: 주전자를 고리에 걸면 서서히 물 온도가 떨어지게 돼.
//화면 중앙 하단(플레이어 대사 위치)으로 이동
<<move_cyrus 0 -3.2>>
키루스: 기억해. 찻잎을 덖고 유념해서 다병에 넣고,
키루스: 60도의 물을 붓고 5초간 우리는 거야.
키루스: 적당한 시간 동안 우렸다면 종을 치는 걸 잊지 마.
키루스: 자, 이제 네가 녹차를 만들어 봐.
키루스: 레시피를 잊었다면, 탭으로 노트를 펼쳐 봐.
===

title: 일차0_밤1
---
<<play_sfx gulp>>
<<change_sprite 키루스 무표정>>
<<play_sfx gulp>>
<<if get_evaluation_result() == "완벽">>
<<change_sprite 키루스 곤란>>
<<affinity_change 키루스 3>>
키루스: ...!
키루스: 크흠, 나쁘지 않아.
키루스: 녹차는 기본 중의 기본이니까.
<<elseif get_tea_name() == "녹차">>
<<change_sprite 키루스 놀람>>
<<affinity_change 키루스 2>>
키루스: ...
키루스: 적어도 녹차는 만들었네.
키루스: 나쁘진 않지만...
키루스: 연습은 더 필요해 보이는군.
<<elseif get_tea_name() != "녹차" and get_tea_name() != "알수없는차">>
<<affinity_change 키루스 2>>
<<change_sprite 키루스 놀람>>
키루스: ...!
키루스: 나쁘지 않아.
<<change_sprite 키루스 무표정>>
키루스: 정작 내가 주문한 녹차는<br>나오지 않았지만 말이야.
<<else>>
<<affinity_change 키루스 -2>>
<<change_sprite 키루스 분노>>
키루스: 이건 뭐지?
키루스: 난 녹차를 달라고 했지, 음식물 쓰레기를 주문한 적은 없는데.
키루스: ...하. 당장 치워버리도록 해.
키루스: 갈 길이 멀군...
<<endif>>
<<change_sprite 키루스 무표정>>
키루스: 좋은 차를 내주면, 손님은 알아서 돈을 낼 거야.
<<if get_tea_name() == "녹차">>
<<pay>>
<<change_sprite 키루스 미소>>
키루스: 이렇게 말이지.
<<endif>>
키루스: 이상한 차를 내주면 은화는커녕 욕을 듣게 되겠지.
키루스: 그러니까, 망자들이 만족할 수 있는 차를 내주도록 해.
키루스: 그리고 말이야...
키루스: 차를 우리는 동안 네 서류를 봤어.
키루스: 죄명이 꽤 화려하던데?
키루스: 하지만 이름은 공란이더군.
키루스: 서류가 몇 칸 비어있다고 해서 내게 큰 타격이 있는 건 아니지만,
키루스: 선배 중에 빈칸을 혐오하는 사람이 한 명 있거든.
키루스: 사람 한 명 살린다 치고 이름을 알려주지 않겠어?
-> 기억이 안 나요.
-> 이름? 먹는 건가?
    <<change_sprite 키루스 분노>>
    키루스: 또 엉뚱한 소리를 하는군.
    키루스: 장난치지 마.
    -> 정말이에요.
    <<change_sprite 키루스 무표정>>
키루스: 그래... 그럴 수 있지.
키루스: 하지만 넌 이제 이 찻집의 주인인 셈이고,
키루스: 난 찻집 주인을 계속 '너'라고 부를 만큼 배짱이 좋진 못해서 말이야.
키루스: 뭐든 좋으니 이름을 적어주면 좋겠는데.
-> 내 이름은...
    <<set_player_nickname>>
    <<change_sprite 키루스 미소>>
    키루스: 그래. 고마워, {$playerName}.
    키루스: 네 표정을 보니 알겠네. 기억이 전혀 없는 건가?
    -> 전혀요.
    -> 몇몇 장면들은 떠오르지만...
<<change_sprite 키루스 무표정>>
키루스: 그것 참 곤란하겠어.
키루스: 하지만 너를 이 구역에 파견한 데에는 이유가 있을 거야.
키루스: 심판관들은 아무 생각 없이 망자들을 마주하지 않으니까.
<<change_sprite 키루스 미소>>
키루스: 그래서, 죄를 잊은 죄인이 된 기분은 어때?
-> 심판관?
    키루스: 죽어서 이곳에 오게 된 망자들은 일곱 번의 심판을 받아.
    키루스: 그 심판을 주관하는 사람이 심판관이지.
    키루스: 죗값을 치른 망자는 새 삶을 시작하게 되는 거고.
-> 죄를 잊은 죄인?
    키루스: 네가 윤회를 거부당한 망자라고 했던 거, 기억나?
    키루스: 일곱 번의 심판이 모두 끝나고,
    키루스: 죗값을 치른 망자들은 윤회를 통해 새 삶을 시작해.
<<change_sprite 키루스 무표정>>
키루스: 하지만 그 기회를 박탈당하고 기억까지 잃은 채 여기로 오는 게...
키루스: 흔한 경우는 아니지.
-> 예를 들면?
키루스: 예를 들자면 죗값을 치르는 걸 거부했다거나,
키루스: 심판관들을 속이다 걸렸다거나.
키루스: 그들의 단순한 변덕일지도 모르고.
키루스: 뭐, 나도 내 할 일이 있어.
키루스: 여기서 계속 업무를 피하고 있을 순 없겠지.
키루스: 내일부터 망자들이 몰려들 거야.
키루스: 잘 대접해주도록 해.
키루스: 망각제를 주는 거 잊지 말고.
키루스: 알아서 잘 할 거라 믿어.
<<exit 키루스>>
<<wait 1>>
<<zoom 0>>
<<wait 1>>
<<finish_night>>
===

title: 일차1_밤
---
<<enter_and_sit 나란 4>>
<<change_sprite 나란 미소>>
<<zoom 4>>
<<wait 2>>
나란: 안녕하세요!
<<change_sprite 나란 곤란>>
나란: 어… 찻집 주인이 벌써 온 줄은 몰랐네요.
나란: 나란이에요. 편하게 불러주세요!
-> 특이한 이름이네.
    <<change_sprite 나란 기쁨>>
	<<affinity_change 나란 1>>
	나란: 그런가요? 좋은 의미겠죠? 고마워요!
-> 그래.
나란: 실례가 아니라면 이름을 물어도 될까요?
-> {$playerName}.
나란: 반가워요, {$playerName}.
나란: 좋은 날이네요. 그렇죠?
<<change_sprite 나란 곤란>>
나란: 아, 하하. 사실...
나란: 이렇게 일찍 후임자가 도착할 줄은 몰랐어요.
나란: ... ...
-> 죽은 사람? 
    Player: 너도 망자인 거지?
    <<change_sprite 나란 놀람>>
    나란: 네?! 오...
    <<change_sprite 나란 슬픔>>
    나란: 그렇다고 할 수 있죠.
    나란: 아직 숨 쉬고 있는 사람이 이곳에 있을 리 없으니까요.
    <<change_sprite 나란 무표정>>
    -> 기억을 잃었어.
        Player: 기억을 잃었어.
        <<change_sprite 나란 놀람>>
        Player: 정신을 차렸을 땐 이 섬으로 오는 열차에 타 있었고.
    -> 열차에서 깨어났어.
        Player: 기억을 잃고 열차에서 깨어났거든.
        <<change_sprite 나란 놀람>>
    나란: 세상에, 어쩌다가요?!
    Player: 신만이 알겠지.
-> 여기엔 어쩌다? 
    Player: 이 찻집엔 어떻게 온 거야?
    <<change_sprite 나란 놀람>>
    <<wait 2>>
    <<change_sprite 나란 곤란>>
    나란: 어, 그러니까...
    나란: 전 원래 차를 좋아하거든요!
<<change_sprite 나란 무표정>>
나란: 오늘이 첫 영업이었나요?
<<change_sprite 나란 미소>>
나란: 크게 어려운 점은 없었죠?
-> 사라진 망각제?
<<change_sprite 나란 놀람>>
나란: 네?! 망각제 말이죠...?
<<change_sprite 나란 곤란>>
나란: 죄송하지만 저는 잘 모르겠어요.
나란: 차사분들께 묻는 편이 좋지 않을까요?
-> 실례했어.
Player: 실례했어. 망자들이 망각제를 요청하는데,
Player: 어디에서도 찾을 수가 없어서.
Player: 주문할래?
<<change_sprite 나란 놀람>>
나란: 네!!
나란: 그, 그럼 우선 한 잔만 부탁드릴게요.
<<change_sprite 나란 미소>>
-> 알겠어.
-> 어떤 걸로 줄까?
나란: 밖이 꽤 추워서…
나란: 우선 물 한 잔을 마시고 싶어요.
<<change_sprite 나란 놀람>>
나란: 앗, 따뜻한 걸로요!
<<add_success_tea hotwater>>
<<order 일차1_밤1>>
===


title: 일차1_밤1
---
<<play_sfx gulp>>
<<if get_tea_name() == "뜨거운물">>
<<affinity_change 나란 2>>
<<change_sprite 나란 기쁨>>
나란: 고마워요.
<<wait 1>>
나란: 정말 따뜻하네요!
<<elseif get_tea_name() != "뜨거운물" and get_tea_name() != "알수없는차">>
<<affinity_change 나란 -2>>
<<change_sprite 나란 무표정>>
나란: 음…
나란: 따뜻한 물은 아니지만, 괜찮아요.
나란: 잘 마실게요.
<<else>>
<<affinity_change 나란 -2>>
<<change_sprite 나란 무표정>>
나란:...이건 뭔가요?
    -> 미안.
        나란: 괜찮아요! 처음이라면 누구나 실수할 수 있죠.
        <<change_sprite 나란 슬픔>>
        나란: ...
        <<wait 3>>
        Player: 무슨 일 있어?
        <<change_sprite 나란 미소>>
        나란: 아, 하하. 아무것도 아니에요!
    -> 그냥 마시도록 해.
        나란: 그래도 이건...
        나란: 좀 힘들 것 같아요.
<<change_sprite 나란 미소>>
나란: 한번 더 만들어 주실래요?
-> 그래.
<<add_success_tea hotwater>>
<<order 일차1_밤1_다시>>
<<endif>>
<<jump 일차1_밤2>>
===

title: 일차1_밤1_다시
---
<<play_sfx gulp>>
<<if get_tea_name() == "뜨거운물">>
<<affinity_change 나란 2>>
<<change_sprite 나란 기쁨>>
나란: 고마워요.
<<play_sfx gulp>>
나란: 정말 따뜻하네요!
<<elseif get_tea_name() != "뜨거운물" and get_tea_name() != "알수없는차">>
<<affinity_change 나란 -2>>
<<change_sprite 나란 무표정>>
나란: 음…
나란: 따뜻한 물은 아니지만, 괜찮아요.
<<change_sprite 나란 미소>>
나란: 잘 마실게요.
<<else>>
<<affinity_change 나란 -2>>
<<change_sprite 나란 슬픔>>
나란: 제가 어려운 주문을 했다고 생각하진 않았는데...
나란: 괜찮아요, 그럴 수 있죠.
<<endif>>
<<jump 일차1_밤2>>
===

title: 일차1_밤2
---
나란: ... ...
나란: 사실 말씀드릴 게 하나 있어요.
-> 뭔데?
나란: {$playerName} 씨는 믿을 만한 분인 것 같아서...
나란: 제가 가져온 게 있는데요,
<<enter_and_sit 키루스 2 >>
<<change_sprite 키루스 무표정>>
<<change_sprite 나란 놀람>>
<<zoom 3>>
<<wait 2>>
키루스: 좋은 저녁이야. 어? 너도 있었군.
나란: 키, 키루스 씨?!
키루스: 차를 안 좋아한다고 하지 않았었나?
<<change_sprite 나란 곤란>>
나란: 아하하... 새로운 취미를 만들어볼까 해서요...
키루스: 그렇군.
키루스: 그래서, 오늘 첫 영업은 어떻게,
<<change_sprite 나란 놀람>>
나란: <size=1.5em><b>자, 잠깐만요!!</b></size>
나란: <b>급한 일이 방금 생각나서, 전 가볼게요!</b>
나란: <b>오늘, 정말 즐거웠어요!</b>
<<change_sprite 나란 곤란>>
<<exit 나란>>
<<change_sprite 키루스 놀람>>
<<wait 2>>
<<zoom 2>>
<<wait 2>>
키루스: ...
키루스: 왜 저러는 거지? 
키루스: 이유를 알고 있어?
-> 글쎼요...
-> 잘 모르겠어요.
<<change_sprite 키루스 무표정>>
키루스: 뭐. 아마 또 별것 아닌 이유겠지.
키루스: 아까 말했듯, 영업이 성공적으로 끝났는지 확인하려고 왔어.
키루스: 귀찮더라도 참아.
키루스: 이 찻집은 내 담당이거든.
<<change_sprite 키루스 미소>>
키루스: 얘기하기 전에, 차 한 잔 받을 수 있을까?
-> 어떤 차를 드릴까요?
키루스: 오늘은... 녹차, 아니. 백차가 좋겠어.
키루스: 이제 백차 만드는 법은 알고 있겠지?
키루스: 어제도 말했듯, 모르겠다면 노트를 참고하도록 해.
<<add_success_tea whitetea>>
<<order 일차1_밤3>>
===

title: 일차1_밤3
---
<<change_sprite 키루스 무표정>>
<<play_sfx gulp>>
<<if get_evaluation_result() == "완벽">>
<<change_sprite 키루스 곤란>>
<<affinity_change 키루스 2>>
키루스: 좋아.
키루스: 난 항상 녹차를 마시는 편이지만, 역시 백차도 나쁘지 않네.
키루스: 어제보단 실력이 좋아진 것 같아.
<<elseif get_tea_name() == "백차">>
<<change_sprite 키루스 미소>>
<<affinity_change 키루스 2>>
키루스: 음, 완벽하진 않지만...
키루스: 그다지 나쁘진 않네.
<<elseif get_tea_name() == "녹차">>
<<change_sprite 키루스 놀람>>
<<affinity_change 키루스 2>>
키루스: 녹차군.
키루스: 백차를 주문했던 걸로 기억하는데.
키루스: 물론, 녹차는 언제나 환영이지.
<<elseif get_tea_name() != "백차" and get_tea_name() != "알수없는차">>
<<affinity_change 키루스 1>>
<<change_sprite 키루스 놀람>>
키루스: 일부러 이러는 건가?
<<change_sprite 키루스 무표정>>
키루스: 맛이 나쁘지 않다는 건 인정하지.
<<else>>
<<affinity_change 키루스 -2>>
<<change_sprite 키루스 분노>>
키루스: ...하.
키루스: 정말 웃기는 망자군.
키루스: 이런 걸 먹으라고 내놓은 건 아니겠지.
<<endif>>
<<change_sprite 키루스 무표정>>
키루스: 그나저나, 별 일 없었나?
-> 망각제가 없어요.
<<change_sprite 키루스 놀람>>
키루스: 뭐?
키루스: 그럴리가.
키루스: 잘 살펴본 거 맞아? 파랗고 특이하게 생긴 병 말이야.
-> 정말 없는데요.
<<change_sprite 키루스 무표정>>
키루스: 주방으로 가자.
키루스: 내가 확인하지.
<<go_to_kitchen 일차1_주방>>
===

title: 일차1_주방
//대사창 플레이어와 같은 위치, 가운데 하단 고정 오직 키루스만 말함
---
키루스: 자, 저 선반을 봐.
키루스: ...?
키루스: 분명 저기에...
키루스: 잠깐.
<<move_camera>>
<<move_cyrus -25.6 -3>>
<<wait 1>>
키루스: 설마...
<<wait 1>>
<<move_camera>>
<<move_cyrus 0 -3.2>>
<<wait 1>>
키루스: 빌어먹을, 정말 사라졌군.
<<go_teahouse_front 일차1_밤4>>
===

title: 일차1_밤4
---
<<change_sprite 키루스 놀람>>
<<affinity_change 키루스 -2>>
키루스: ...
키루스: 처음부터 없었던 건가? 기억 나?
    -> 기억이 잘...
        키루스: 이건 정말 곤란하네.
    -> 없었어요.
        키루스: 그렇군. 알겠어.
<<change_sprite 키루스 무표정>>
키루스: 고작 며칠이 지났다고...
키루스: 아마 망자의 짓일 거야.
키루스: 예상되는 인물이 몇 있어.
키루스: 내일 찾아보도록 할게.
키루스: 하, 또 피곤한 일이 늘었군.
<<exit 키루스>>
<<wait 1>>
<<zoom 0>>
<<wait 1>>
<<finish_night>>
===